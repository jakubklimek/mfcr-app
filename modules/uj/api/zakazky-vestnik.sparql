PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX pc: <http://purl.org/procurement/public-contracts#>
PREFIX pccz: <http://purl.org/procurement/public-contracts-czech#>
PREFIX gr: <http://purl.org/goodrelations/v1#>

PREFIX mfcr: <http://linked.opendata.cz/ontology/domain/mfcr/monitor/ciselniky/>
PREFIX govr: <http://linked.opendata.cz/ontology/domain/seznam.gov.cz/rejstriky/>

PREFIX app: <http://linked.opendata.cz/mfcr/ontology/application/>

PREFIX resource: <{{"param": "resource"}}>

CONSTRUCT {

  ?rZakazka a pc:Contract ;
    dcterms:title ?tZakazka ;
    pc:awardedTender ?rAwardedTender .

  ?rAwardedTender a pc:Tender ;
    pc:supplier ?rSupplier ;
    app:priceVAT ?vVATPrice ;
    app:priceNoVAT ?vNoVATPrice ;
    app:ePriceNoVAT ?vENoVATPrice .

  ?rSupplier a gr:BusinessEntity ;
    gr:legalName ?tSupplier .

} WHERE {

  {
    SELECT DISTINCT ?rZakazka ?tZakazka
    WHERE {  
      ?rUjv a mfcr:UcetniJednotka ;
        mfcr:versionOf resource: ;
        owl:sameAs ?rBe .
    
    GRAPH <http://linked.opendata.cz/resource/dataset/isvz/zakazky> {
      ?rZakazka pc:contractingAuthority ?rBe ;
        dcterms:title ?tZakazka .
    }

    }
    ORDER BY ?tZakazka
	LIMIT {{"param" : "limit", "default":"5"}}
	OFFSET {{"param" : "offset", "default":"0"}}
  }

  {
    ?rZakazka pc:awardedTender ?rAwardedTender .

    OPTIONAL { 
             ?rAwardedTender pc:supplier ?rSupplier . 
             ?rSupplier gr:legalName ?tSupplier .
    }
    
    OPTIONAL {{
      ?rAwardedTender pc:actualPrice ?rNoVATPrice .
      ?rNoVATPrice
        gr:valueAddedTaxIncluded ?rNoVATPriceIncluded ;
        gr:hasCurrencyValue ?vNoVATPrice.
      FILTER(?rNoVATPriceIncluded = false)
    } UNION {
      ?rAwardedTender pc:actualPrice ?rVATPrice .
      ?rVATPrice
        gr:valueAddedTaxIncluded ?rVATPriceIncluded  ;
        gr:hasCurrencyValue ?vVATPrice.
      FILTER(?rVATPriceIncluded = true)
    }}

    OPTIONAL {
      ?rAwardedTender pc:estimatedPrice ?rENoVATPrice .
      ?rENoVATPrice
        gr:hasCurrencyValue ?vENoVATPrice.
    }

  } 
  
}
